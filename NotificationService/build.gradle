plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.springframework.boot" version "2.2.0.RELEASE"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"
//    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.70'
    id 'idea'
    id 'jacoco'
    id "org.openapi.generator" version "5.1.0"
}

group 'org.example'
version '1.0-SNAPSHOT'
def generatedSourcesDir = file("$buildDir/generated/notification-service/src/main/kotlin")

repositories {
    mavenCentral()
}


idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/kotlin/main")

        generatedSourceDirs += generatedSourcesDir
    }
}

sourceSets {
    main {
        kotlin {
            srcDirs += generatedSourcesDir
        }
        java {
            srcDirs += generatedSourcesDir
        }
    }
}


configurations {
    developmentOnly
    ktlint
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.4.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // DB drivers
    implementation 'org.postgresql:postgresql'
    developmentOnly 'com.h2database:h2'
    testCompile 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //formatting
    ktlint "com.pinterest:ktlint:0.36.0"
    //AWS
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.228')
    implementation 'com.amazonaws:aws-java-sdk-sns'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-ses', version: '1.11.979'

    //OPENAPI
    implementation(fileTree("$buildDir/generated/libs") { include("*.jar") })
    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation "io.springfox:springfox-swagger-ui:2.9.2"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    compile 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-validation"


}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

task buildApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    def inputSpecification = "$rootDir/src/main/resources/spec.yaml"
    def outputDirectory = "$buildDir/generated/notification-service/"

    generatorName = "kotlin-spring"
    inputSpec = inputSpecification.toString()
    outputDir = outputDirectory.toString()
    apiPackage = "web.service.notifications"
    modelPackage = "web.service.notifications.model"
    ignoreFileOverride = "$rootDir/.openapi-generator-ignore"
    modelNameSuffix = "DTO"

    inputs.files(inputSpecification)
    outputs.dir(outputDirectory)

    configOptions = [
            artifactId        : "notification-service",
            serviceInterface  : "true",
            swaggerAnnotations: "true",
            serializableModel : "true",
            skipValidateSpec  : "false",
            interfaceOnly     : "false",
            enumPropertyNaming: "UPPERCASE",
            groupId           : "web.service",
            basePackage       : "web.service.notifications"
    ]
}

task assembleApiClient(type: Copy) {
    from("build/generated") {
        include("*/build/libs/*.jar")
    }
    into("build/generated/libs/")
    includeEmptyDirs = false
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xinline-classes"]
        jvmTarget = "1.8"
    }
}
compileKotlin.dependsOn buildApi